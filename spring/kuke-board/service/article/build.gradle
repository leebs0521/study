ext {
    // QueryDSL Jakarta (Hibernate 6 호환)
    querydslVersion = '5.0.0'
}

// QueryDSL QClass 경로
def generated = "$buildDir/generated/querydsl"

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.mysql:mysql-connector-j'
    implementation project(":common:snowflake")
    implementation project(':common:outbox-message-relay')
    implementation project(':common:event')

    implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api:3.1.0"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api:2.1.1"
}

sourceSets {
    main {
        java {
            srcDirs += [generated]
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.annotationProcessorGeneratedSourcesDirectory = file(generated)
    options.compilerArgs += ["-parameters"]
}

tasks.named('clean') {
    doFirst { delete file(generated) }
}